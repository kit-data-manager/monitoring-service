plugins { 
    id "org.springframework.boot" version "2.7.5"
    id "io.spring.dependency-management" version "1.1.0" 
    id "io.freefair.lombok" version "6.5.1"
    id "io.freefair.maven-publish-java" version "6.5.1"
    id "com.github.kt3k.coveralls" version "2.12.0" 
    id "org.owasp.dependencycheck" version "7.3.0"
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id "net.researchgate.release" version "3.0.2"
    id 'java'
    id 'jacoco'
}

jar {
    baseName = 'monitoring-service'
    version = System.getenv('version')
}

repositories {
    mavenLocal() 
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
 
//configurations {
//    all*.exclude module : 'spring-boot-starter-logging'
//}

ext {
    set('springBootVersion', "2.7.5")
    set('springDocVersion', "1.6.9")
    set('keycloakVersion', "19.0.0")
    set('springCloudVersion', "2021.0.3")

    // directory for generated code snippets during tests
    snippetsDir = file("build/generated-snippets")
}

println "Running gradle version: $gradle.gradleVersion"
println "Building ${name} version: ${version}"
println "JDK version: ${JavaVersion.current()}"

if (System.getProperty('profile') == 'minimal') {
    println 'Using minimal profile for building ' + project.getName()
    apply from: 'gradle/profile-minimal.gradle'   
} else {
    println 'Using default profile executing all tests for building ' + project.getName()
    apply from: 'gradle/profile-complete.gradle'
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-mail"
    implementation "de.codecentric:spring-boot-admin-starter-server:2.7.10"
    implementation "org.springframework.cloud:spring-cloud-starter"
    implementation "org.keycloak:keycloak-spring-boot-starter:${keycloakVersion}"
    implementation "org.keycloak:keycloak-admin-client:${keycloakVersion}"

 
    //Java 11 Support 
    implementation "javax.xml.bind:jaxb-api:2.3.1"
       
    //Java 11 Support 
    testImplementation "org.mockito:mockito-inline:4.9.0"
    testImplementation "junit:junit:4.13.2"
}

dependencyManagement {
    imports { mavenBom("org.keycloak.bom:keycloak-adapter-bom:${keycloakVersion}") }
    imports { mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}") }
}

if (project.hasProperty('release')) {
    println 'Using \'release\' profile for building ' + project.getName()
    apply from: 'gradle/profile-deploy.gradle'
}

test {
    testLogging {
        outputs.upToDateWhen {false}
        showStandardStreams = true
    }
   environment "spring.config.location", "classpath:/test-config/"
}

tasks.withType(Test) {
    testLogging {
        events 'started', 'passed'
    }
}

bootJar {
    println 'Create bootable jar...'
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
    launchScript()
}

// task for printing project name.
task printProjectName {
    doLast {
        println "${project.name}"
    }
}

